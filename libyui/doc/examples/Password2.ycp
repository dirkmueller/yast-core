{
    // Build dialog with two password fields, an "OK" and a "Cancel" button.
    UI::OpenDialog(
	       `VBox( 
		     `Password(`id(`pw1), "Enter password:"),
		     `Password(`id(`pw2), "Confirm password:"),
		     `HBox(
			   `PushButton(`id(`ok),     "&OK"    ),
			   `PushButton(`id(`cancel), "&Cancel")
			   )
		     )
	       );
    any button = nil;
    string pw1 = "";
    string pw2 = "";


    // Input loop. Will be terminated when the same password has been
    // entered in both fields or when 'Cancel' has been clicked.
    repeat
    {
	// Wait for Input.
        button = UI::UserInput();

	// Get the values from both password fields.
	pw1 = (string) UI::QueryWidget(`id(`pw1), `Value );
	pw2 = (string) UI::QueryWidget(`id(`pw2), `Value );

	if ( button != `cancel )
	{
	    if ( pw1 == "" && pw2 == "" )
	    {
		// Error popup if nothing has been entered.
		UI::OpenDialog(
			   `VBox(
				 `Label("You must enter a password."),
				 `PushButton("&OK")
				 )
			   );
		UI::UserInput();
		UI::CloseDialog();
	    }
	    else if ( pw1 != pw2 )
	    {
		// Error popup if passwords differ.
		UI::OpenDialog(
			   `VBox(
				 `Label("The two passwords mismatch."),
				 `Label("Please try again."),
				 `PushButton("&OK")
				 )
			   );
		UI::UserInput();
		UI::CloseDialog();
	    }
	}
    } until ( ( pw1 != "" && pw1 == pw2 ) ||
	      button == `cancel );
    
    UI::CloseDialog();
}
