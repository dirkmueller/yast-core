/**
 * Advanced ColoredLabel example: A sample ColoredLabel widget and sliders
 * for both foreground and background colors and a colored label beside each slider.
 **/
UI::{
    if ( ! HasSpecialWidget(`ColoredLabel) || ! HasSpecialWidget(`Slider) )
    {
	OpenDialog(
		   `VBox( 
			 `Label("Error: This UI doesn't support the required special widgets!"),
			 `PushButton(`opt(`default), "&OK")
			 )
		   );
	UserInput();
	CloseDialog();
	
	return;
    }

    define void sample( integer fg_red, integer fg_green, integer fg_blue,
			integer bg_red, integer bg_green, integer bg_blue ) ``{
	return `ColoredLabel( `opt(`hstretch),
			      "Use the sliders\nto change color",
			      `rgb( fg_red, fg_green, fg_blue ),
			      `rgb( bg_red, bg_green, bg_blue ),
			      30 );
    };
    
    OpenDialog(
	       `VBox(
		     `ReplacePoint(`id(`sample), sample( 200, 0, 0,
							 0, 0, 200) ),
		     `VSpacing(),
		     `HBox(
			   `Frame( "Foreground",
				   `VBox(
					 `HBox(
					       `Slider(`id(`fg_red  ),`opt(`notify), "&red",   0, 255, 200 ),
					       `ColoredLabel("   ", `rgb(255, 0, 0), `rgb(255, 0, 0), 10 ),
					       `HSpacing()
					       ),
					 `HBox(
					       `Slider(`id(`fg_green),`opt(`notify), "&green", 0, 255,   0 ),
					       `ColoredLabel("   ", `rgb(0, 255, 0), `rgb(0, 255, 0), 10 ),
					       `HSpacing()
					       ),
					 `HBox(
					       `Slider(`id(`fg_blue ),`opt(`notify), "&blue",  0, 255,   0 ),
					       `ColoredLabel("   ", `rgb(0, 0, 255), `rgb(0, 0, 255), 10 ),
					       `HSpacing()
					       )
					 )
				   ),
			   `HSpacing(),
			   `Frame( "Backround",
				   `VBox(
					 `HBox(
					       `Slider(`id(`bg_red  ),`opt(`notify), "&red",   0, 255, 0 ),
					       `ColoredLabel("   ", `rgb(255, 0, 0), `rgb(255, 0, 0), 10 ),
					       `HSpacing()
					       ),
					 `HBox(
					       `Slider(`id(`bg_green),`opt(`notify), "&green", 0, 255,   0 ),
					       `ColoredLabel("   ", `rgb(0, 255, 0), `rgb(0, 255, 0), 10 ),
					       `HSpacing()
					       ),
					 `HBox(
					       `Slider(`id(`bg_blue ),`opt(`notify), "&blue",  0, 255, 200 ),
					       `ColoredLabel("   ", `rgb(0, 0, 255), `rgb(0, 0, 255), 10 ),
					       `HSpacing()
					       )
					 )
				   )
			   ),
		     `VSpacing(),
		     `PushButton(`id(`close), "&Close")
		     )
	       );

    symbol widget = nil;

    do
    {
	widget = UserInput();

	if ( widget != `close )
	{
	    ReplaceWidget(`id(`sample),
			  sample( QueryWidget(`id(`fg_red  ), `Value ),
				  QueryWidget(`id(`fg_green), `Value ),
				  QueryWidget(`id(`fg_blue ), `Value ),
				  
				  QueryWidget(`id(`bg_red  ), `Value ),
				  QueryWidget(`id(`bg_green), `Value ),
				  QueryWidget(`id(`bg_blue ), `Value )
				  )
			  );

	}
	
    } while ( widget != `close );
    
    CloseDialog();
}
