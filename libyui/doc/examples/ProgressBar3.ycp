// ProgressBar example with huge numbers
{
    integer max_progress = 0x100000000;
    integer progress = 2;
    
    UI::OpenDialog(
		   `VBox(
			 `ProgressBar(`id(`pr), "Testing progress bar with huge numbers", max_progress, progress ),
			 `HBox(
			       `HWeight( 1, `PushButton(`id(`half), "Half (/2)") ),
			       `HWeight( 1, `PushButton(`id(`double), "Double (*2)") ),
			       `HSpacing(`opt(`hstretch), 4 ),
			       `HWeight( 1, `PushButton(`id(`close), "&Close" ) )
			       )
			 )
		   );
 
    
    while ( true )
    {
	symbol button = (symbol) UI::UserInput();

	if ( button == `double || button == `half )
	{
	    if ( button == `double )
		progress = progress * 2;
	    else if ( button == `half )
		progress = progress / 2;

	    if ( progress < 2 )
		progress = 2;
	    
	    if ( progress > max_progress )
		progress = max_progress;
	    
	    UI::ChangeWidget(`id(`pr), `Value, progress);
	    UI::ChangeWidget(`id(`pr), `Label, sformat("Progress %1 of %2", progress, max_progress ));
	}
	else if ( button == `close )
	    break;
    }
    
    UI::CloseDialog();
}
