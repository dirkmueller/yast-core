// Advanced MultiSelectionBox example:
//
// Get and change the current item. Note that unlike with a SelectionBox, this
// is typically NOT what an application needs: The current item is the item
// that has the keyboard focus. It doesn't need to be selected. Most
// applications will want to use SelectedItems rather than CurrentItem.
{

    UI::OpenDialog(
	       `VBox(
		     `MultiSelectionBox( `id(`toppings), "Select pizza toppings:",
					 [
					  `item( `id(0), "Cheese"	, true  ),
					  `item( `id(1), "Tomatoes"	, true  ),
					  `item( `id(2), "Mushrooms"	, false ),
					  `item( `id(3), "Onions" 	),
					  `item( `id(4), "Salami" 	),
					  `item( `id(5), "Ham"		)
					 ] ),
		     `HBox(
			   `PushButton(`id(`next), "&Next" ),
			   `HStretch(),
			   `PushButton(`id(`cancel), "&Close")
			   )
		     )
	       );

    any widget = nil;

    repeat
	{
	    widget = UI::UserInput();

	    if ( widget == `next )
	    {
		integer current = (integer) UI::QueryWidget(`toppings, `CurrentItem );

		current = current + 1;
		if ( current > 5 )
		    current = 0;

		y2milestone( "Current: %1", current );
		UI::ChangeWidget(`toppings, `CurrentItem, current );
	    }

	} until ( widget == `cancel );

    UI::CloseDialog();
}
