{
    // Demo for postponed shortcut checking

    term b_kilroy 	= `PushButton(`opt(`hstretch), "&Kilroy");
    term b_was		= `PushButton(`opt(`hstretch), "&was");
    term b_here		= `PushButton(`opt(`hstretch), "&here");

    
    UI::OpenDialog(
	       `VBox(
		     `VSpacing( 0.3 ),
		     `HBox(
			   `HSpacing(),
			   `Label("Click on one of the upper buttons\n" +
				  "to exchange them." ),
			   `HSpacing()
			   ),
		     `VSpacing( 0.3 ),
		     `ReplacePoint(`id(1), b_kilroy	),
		     `ReplacePoint(`id(2), b_was	),
		     `ReplacePoint(`id(3), b_here	),
		     `VSpacing(`opt(`vstretch) ),
		     `Right( `PushButton( `id(`cancel), "&Quit" ) )
		     )
	       );


    any button = nil;

    y2milestone( "Initial state: 'Kilroy was here'" );
    button = UI::UserInput(); if ( button == `cancel ) return;

    UI::PostponeShortcutCheck();		// "&Kilroy" "&was" "&here"
    UI::ReplaceWidget(`id(1), b_was    );	// "&was" "&was" "&here"
    UI::ReplaceWidget(`id(2), b_kilroy );	// "&was" "&Kilroy" "&here"
    UI::CheckShortcuts();

    y2milestone( "After change: 'was Kilroy here'" );
    button = UI::UserInput(); if ( button == `cancel ) return;
    
    UI::PostponeShortcutCheck();		// "&was" "&Kilroy" "&here"
    UI::ReplaceWidget(`id(1), b_here   );	// "&here" "&Kilroy" "&here"
    UI::ReplaceWidget(`id(2), b_was    );	// "&here" "&was" "&here"
    UI::ReplaceWidget(`id(3), b_kilroy );	// "&here" "&was" "&Kilroy"
    UI::CheckShortcuts();

    y2milestone( "After change: 'here was Kilroy'" );
    button = UI::UserInput(); if ( button == `cancel ) return;

    UI::PostponeShortcutCheck();		// "&here" "&was" "&Kilroy"
    UI::ReplaceWidget(`id(1), b_kilroy );	// "&Kilroy" "&was" "&Kilroy"
    UI::ReplaceWidget(`id(3), b_here   );	// "&Kilroy" "&was" "&here"
    // Omitting UI::CheckShortcuts();

    y2milestone( "After change: back to 'Kilroy was here'" );
    y2milestone( "Expect complaint about missing UI::CheckShortcuts()" );
    button = UI::UserInput(); if ( button == `cancel ) return;


    y2milestone( "Expect shortcut conflict: '&was' vs. '&was'" );
    UI::ReplaceWidget(`id(1), b_was    );	// "&was" "&was" "&here"
    UI::ReplaceWidget(`id(2), b_kilroy );	// "&was" "&Kilroy" "&here"
    y2milestone( "Expect complaint about excess UI::CheckShortcuts()" );
    UI::CheckShortcuts();

    y2milestone( "After change: 'was Kilroy here'" );
    button = UI::UserInput(); if ( button == `cancel ) return;
    
	
    UI::CloseDialog();
}

