/*
 * stupid puzzle
 * $Id$
 */

{

UI::{

  define DebugDialog(any str) ``{
    OpenDialog(`VBox(`RichText(sformat("%1",str)),`PushButton(_("&OK"))));
    any r = UserInput();
    CloseDialog();
  };
};


define buttons_table (list buttons) ``{

    term tabl = `VBox ();
    term line = `HBox ();
    
    integer i = 0;
    integer j = 0;
    string label = "";
    
    while ((i + line_len - 1) < size (buttons)) {
	
	j    = 0;
	line = `HBox ();
	    
	while (j < line_len) {
	    
	    label = select (buttons, i + j, "");
	    
	    if (size (label) > 0)
		line = add (line, `ReplacePoint (`id(sformat ("rp%1", i + j)), `PushButton (`id (i + j), label)));
	    else
		line = add (line, `ReplacePoint (`id (sformat ("rp%1", i + j)), `HStretch ()));
	    
	    j = j + 1;
	}
	
	tabl = add (tabl, line);
	
	i = i + j;
    }
 
    return tabl;
    
}

define isRight (list buttons, integer pos) ``{
    if (pos + 1 < size (buttons))
	return (select (buttons, pos + 1, "") == "");
    return false;
}

define isLeft (list buttons, integer pos) ``{
    if (pos - 1 >= 0)
	return (select (buttons, pos - 1, "") == "");
    return false;
}

define isDown (list buttons, integer pos) ``{
    if (pos + line_len < size (buttons))
	return (select (buttons, pos + line_len, "") == "");
    return false;
}

define isUp (list buttons, integer pos) ``{
    if (pos - line_len >= 0)
	return (select (buttons, pos - line_len, "") == "");
    return false;
}

/*
 * do the move
 */

define move (list buttons, integer pos, integer m) ``{

    list ret  = [];
    integer i = 0;

    while (i < size (buttons)) {
	
	if (i == pos)
	    ret = add (ret, select (buttons, i + m, nil));
	else if (i == pos + m)
	    ret = add (ret, select (buttons, i - m, nil));
	else
	    ret = add (ret, select (buttons, i, nil));
	
	i = i + 1;
	
    }

    UI::ReplaceWidget (`id(sformat ("rp%1", pos)), `HStretch ());
    UI::ReplaceWidget (`id(sformat ("rp%1", pos + m)), `PushButton (`id (pos + m), select (buttons, pos, nil)));


    return ret;
}

/*
 * generate list of buttons
 * TODO: needs to be randomized
 */
define generate_buttons (integer space) ``{

    list    buttons  = [];
    integer i        = 1;
    
    while (i <= line_len * line_len) {
	
	if (i == space)
	    buttons = add (buttons, "");
	else {
	    if (i < space)
		buttons = add (buttons, sformat ("%1", i));
	    else
		buttons = add (buttons, sformat ("%1", i - 1));
	}
	
	i = i + 1;

    }

    return buttons;
}


define PuzzleWindow(list buttons) ``{
	
	
    UI::OpenDialog (`HBox (
	`HSpacing (3),
	`VBox (
	    `VSpacing (),
	    `Frame (_("Puzzle"), buttons_table (buttons)),
	    `VSpacing (5),
	    `PushButton (`id (`quit), _("&Quit"))),
	`HSpacing ())
	);
    
    any ui = nil;
	
    repeat {
	
	ui = UI::UserInput();

	if (is (ui, integer)) {

  	    if      (isLeft (buttons, ui))  buttons = move (buttons, ui, -1);
  	    else if (isRight (buttons, ui)) buttons = move (buttons, ui, 1);
  	    else if (isUp (buttons, ui))    buttons = move (buttons, ui, -line_len);
  	    else if (isDown (buttons, ui))  buttons = move (buttons, ui, line_len);
	}
	

    } until (ui == `cancel || ui == `quit);

    UI::CloseDialog();
	
}

    

// ==== main

// define this global variable to provide nxn puzzle table

integer line_len = 4;

// create buttons with space at num
list buttons  = generate_buttons (5);

// play puzzle		
PuzzleWindow (buttons);
    
}
