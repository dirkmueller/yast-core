{
    // Build dialog with one input field field and an OK button.
    UI::OpenDialog(
	       `VBox( 
		     `InputField(`id(`name), "You will never see this:"),
		     `PushButton("&OK")
		     )
	       );

    // Set an initial value for the input field field.
    UI::ChangeWidget(`id(`name), `Value, "Averell Dalton");
    
    // Change the input field field's label.
    UI::ChangeWidget(`id(`name), `Label, "Name:");
    
    // Wait for user input.
    UI::UserInput();

    // Get the input from the input field field.
    //
    // Notice: The return value of UI::UserInput() does NOT return this value!
    // Rather, it returns the ID of the widget (normally the PushButton)
    // that caused UI::UserInput() to return.
    string name = (string) UI::QueryWidget(`id(`name), `Value);
    
    // Close the dialog.
    // Remember to read values from the dialog's widgets BEFORE closing it!
    UI::CloseDialog();

    
    // Pop up a new dialog to echo the input.
    UI::OpenDialog(
	       `VBox(
		     `Label("You entered:"),
		     `Label(name),
		     `PushButton("&OK")
		     )
	       );
    UI::UserInput();
    UI::CloseDialog();
}
