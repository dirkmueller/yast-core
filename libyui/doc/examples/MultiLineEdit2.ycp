{
    // Build dialog with one multi line edit field and an OK button.
    UI::OpenDialog(
	       `VBox(
		     `HSpacing(60),		// force width
		     `HBox(
			   `VSpacing(7),	// force height
			   `MultiLineEdit(`id(`problem),
					  "Problem &description:",	// label
					  "No problem here")		// initial value
			   ),
		     `PushButton("&OK")
		     )
	       );
    
    // Wait for user input.
    UI::UserInput();

    // Get the input from the MultiLineEdit.
    //
    // Notice: The return value of UI::UserInput() does NOT return this value!
    // Rather, it returns the ID of the widget (normally the PushButton)
    // that caused UI::UserInput() to return.
    string input = (string) UI::QueryWidget(`id(`problem), `Value);
    
    // Close the dialog.
    // Remember to read values from the dialog's widgets BEFORE closing it!
    UI::CloseDialog();

    // Pop up a new dialog to echo the input.
    UI::OpenDialog(
	       `VBox(
		     `Label("You entered:"),
		     `Label(input),
		     `PushButton("&OK")
		     )
	       );
    UI::UserInput();
    UI::CloseDialog();
}
