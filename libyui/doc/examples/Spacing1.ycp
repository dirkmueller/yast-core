{
    // Build dialog with one input field field, 4 Beatles buttons and an OK button.
    UI::OpenDialog(
	       `VBox( 
		     `VSpacing(),
		     `HBox(
			   `Label("Name:"),
			   `InputField(`id(`name), "")
			   ),
		     `VSpacing(0.2),
		     `HBox(
			   `PushButton(`id(`john),	"&John"	),	`HSpacing(0.5),
			   `PushButton(`id(`paul),	"&Paul"	),	`HSpacing(3),
			   `PushButton(`id(`george),	"&George"),	`HSpacing(0.5),
			   `PushButton(`id(`ringo),	"&Ringo"	)
			   ),
		     `VSpacing(0.5),
		     `PushButton(`id(`ok), "&OK")
		     )
	       );

    // Wait for user input.
    any button = nil;

    // Input loop that only the OK button will leave.
    // The 4 Beatles buttons will just propose a name.
    repeat
    {
        button = UI::UserInput();

	if      ( button == `john )	UI::ChangeWidget(`id(`name), `Value, "John Lennon");
	else if ( button == `paul )	UI::ChangeWidget(`id(`name), `Value, "Paul McCartney");
	else if ( button == `george )	UI::ChangeWidget(`id(`name), `Value, "George Harrison");
	else if ( button == `ringo )	UI::ChangeWidget(`id(`name), `Value, "Ringo Starr" );
	
    } until ( button == `ok );

    UI::CloseDialog();
}
