UI::{

    /**
     * Return a text summary of existing buttons.
     **/
    define Summary() ``{
	
	string summary = 
	    ( WidgetExists(`id(`top    ) ) ? "Top button exists"      : "No top button"      ) +
	    ( WidgetExists(`id(`center ) ) ? "\nCenter button exists" : "\nNo center button" )	+
	    ( WidgetExists(`id(`bottom ) ) ? "\nBottom button exists" : "\nNo bottom button" );

	return summary;

    };

    /**
     * Remove button with given id and update summary.
     **/
    define RemoveButton( term id ) ``{
	ReplaceWidget( id, `Empty() );
	ChangeWidget( `id(`summary), `Value, Summary() );
	RecalcLayout();
    }


    /**
     * main
     **/
    
    OpenDialog(
	       `VBox(
		     `ReplacePoint( `id(`rp_top    ), `PushButton(`id(`top    ), "Top Button"    ) ),
		     `ReplacePoint( `id(`rp_center ), `PushButton(`id(`center ), "Center Button" ) ),
		     `ReplacePoint( `id(`rp_bottom ), `PushButton(`id(`bottom ), "Bottom Button" ) ),
		     `VSpacing( 1 ),
		     `Label(`id(`summary), "" ),
		     `VSpacing( 1 ),
		     `PushButton(`id(`close), "&Close")
		     )
	       );

    // Better wait until now before doing the summary - it isn't much use before OpenDialog()
    // since of course all WidgetExists() calls return false until then.
    ChangeWidget( `id(`summary), `Value, Summary() );
    RecalcLayout();
    
    any button = nil;

    repeat
    {
	button = UserInput();

	if	( button == `top    ) 	RemoveButton(`id(`rp_top    ) );
	else if	( button == `center )	RemoveButton(`id(`rp_center ) );
	else if	( button == `bottom )	RemoveButton(`id(`rp_bottom ) );
    } until ( button == `close );

    CloseDialog();
}
