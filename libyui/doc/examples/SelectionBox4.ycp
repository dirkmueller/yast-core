UI::{
    // Create a selection box with three entries.
    // All entries have IDs to identify them independent of locale
    // (The texts might have to be translated!).
    // Entry "Funghi" will be selected by default.
    //
    // There are two buttons to select a "Today's special" and a
    // "veggie" pizza to demonstrate how to select list entries
    // from within a YCP script - even without having to use item IDs.
    OpenDialog(
	       `VBox(
		     `SelectionBox(`id(`pizza),
				   "Select your Pizza:",
				   [
				    "Napoli",
				    "Funghi",
				    "Salami",
				    "Quattro Stagioni",
				    "Caprese",
				    "Speciale",
				    "Hawaii"
				   ] ),
		     `HBox(
			   `PushButton(`id(`todays_special), `opt(`hstretch), "&Today's special" ),
			   `PushButton(`id(`veggie),         `opt(`hstretch), "&Veggie" )
			   ),
		     `PushButton(`id(`ok), `opt(`default), "&OK")
		     )
	       );

    any id = nil;
    
    repeat
	{
	    id = UserInput();

	    if      ( id == `todays_special ) ChangeWidget( `id( `pizza ), `CurrentItem, "Napoli" );
	    else if ( id == `veggie         ) ChangeWidget( `id( `pizza ), `CurrentItem, "Funghi" );
	} until ( id == `ok );

    // Get the input from the selection box.
    //
    // Notice: The return value of UserInput() does NOT return this value!
    // Rather, it returns the ID of the widget (normally the PushButton)
    // that caused UserInput() to return.
    string pizza = QueryWidget(`id(`pizza), `CurrentItem);
    
    // Close the dialog.
    // Remember to read values from the dialog's widgets BEFORE closing it!
    CloseDialog();

    
    // Pop up a new dialog to echo the selection.
    OpenDialog(
	       `VBox(
		     `Label("Pizza " + pizza + " coming right up"),
		     `PushButton(`opt(`default), "&OK")
		     )
	       );
    UserInput();
    
    CloseDialog();
}
