// Advanced ComboBox / SelecionBox example:
// Dialog with a ComboBox and a SelectionBox
// where selecting an item in one will also select it in the other
{
    list items =
	[
	 `item(`id( "Napoli"     ), "Napoli"       ),
	 `item(`id( "Funghi"     ), "Funghi", true ),
	 `item(`id( "Salami"	 ), "Salami"       )
	 ];

    UI::OpenDialog(
		   `VBox(
			 `MinSize( 30, 5,
				   `HBox(
					 `Top(
					      `ComboBox(`id(`pizzaComboBox), `opt(`notify), "Pizza:", items )
					      ),
					 `SelectionBox (`id(`pizzaSelBox  ), `opt(`notify), "Pizza:", items )
					 )
				   ),
			 `Right(`PushButton(`id(`cancel), "&Close") )
			 )
		   );

    symbol widget = nil;

    repeat
	{
	    widget = (symbol) UI::UserInput();
	    // y2debug( "Event widget: %1", widget );

	    if ( widget == `pizzaComboBox ||  widget == `pizzaSelBox )
	    {
		string pizza = (string) UI::QueryWidget( widget, `Value );
		y2debug( "Pizza: %1", pizza );

		UI::ChangeWidget( widget == `pizzaComboBox ?
				  `pizzaSelBox : `pizzaComboBox,
				  `Value, pizza );

		y2debug( "ComboBox: %1", UI::QueryWidget(`pizzaComboBox, `Items ) );
		y2debug( "SelBox  : %1", UI::QueryWidget(`pizzaSelBox,   `Items ) );
	    }
	} until ( widget == `cancel );

    UI::CloseDialog();
}
