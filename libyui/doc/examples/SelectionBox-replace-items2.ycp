// Example showing how to replace SelectionBox items
{

    list pizza_list =
	[
	 `item(`id("Pizza #01"), "Pizza Napoli" ),
	 `item(`id("Pizza #02"), "Pizza Funghi" ),
	 `item(`id("Pizza #03"), "Pizza Salami", true ),
	 `item(`id("Pizza #04"), "Pizza Hawaii" )
	 ];

    list pasta_list =
	[
	 `item(`id("Pasta #11"), "Spaghetti"  ),
	 `item(`id("Pasta #12"), "Rigatoni", true   ),
	 `item(`id("Pasta #13"), "Tortellini" )
	 ];

    UI::OpenDialog( `VBox(
			  `SelectionBox(`id(`menu), "Daily &Specials:", pizza_list ),
			  `HBox(
				`PushButton(`id(`pizza), "Pi&zza" ),
				`PushButton(`id(`pasta), "&Pasta" )
				),
			  `PushButton(`id(`ok), "&OK" )
			  )
		    );

    symbol button = nil;

    do
    {
	button = (symbol) UI::UserInput();

	if ( button == `pizza )		UI::ChangeWidget(`menu, `Items, pizza_list );
	if ( button == `pasta )		UI::ChangeWidget(`menu, `Items, pasta_list );
	
    } while ( button != `ok );

    string order = (string) UI::QueryWidget(`menu, `CurrentItem );
    UI::CloseDialog();


    //
    // Show the result
    //
    
    UI::OpenDialog(`VBox(
			 `Label( sformat( "Your order: %1", order ) ),
			 `PushButton(`opt(`default), "&OK" )
			 )
		   );
    UI::UserInput();
    UI::CloseDialog();
}
