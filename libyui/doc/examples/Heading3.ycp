UI::{
    // Build dialog with one label, 4 Beatles buttons and an OK button.
    OpenDialog(
	       `VBox(
		     `Label("My favourite Beatle:"),
		     // `Heading(`id(`favourite), "Press one of the buttons below"),
		     `Heading(`id(`favourite), "(please select one)"),
		     `HBox(
			   `PushButton(`id(`john),	"John"	),
			   `PushButton(`id(`paul),	"Paul"	),
			   `PushButton(`id(`george),	"George"),
			   `PushButton(`id(`ringo),	"Ringo"	)),
		     `PushButton(`id(`ok), "&OK")
		     )
	       );

    // Wait for user input.
    any button = nil;

    // Input loop that only the OK button will leave.
    // The 4 Beatles buttons will just propose a name.
    repeat
    {
        button = UserInput();

	if      ( button == `john )	ChangeWidget(`id(`favourite), `Value, "John Lennon");
	else if ( button == `paul )	ChangeWidget(`id(`favourite), `Value, "Paul McCartney");
	else if ( button == `george )	ChangeWidget(`id(`favourite), `Value, "George Harrison");
	else if ( button == `ringo )	ChangeWidget(`id(`favourite), `Value, "Ringo Starr" );

    } until ( button == `ok );
}
