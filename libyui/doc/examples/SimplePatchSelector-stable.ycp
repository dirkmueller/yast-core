// Full-fledged simple patch selection
{
    textdomain "bogus";

    // Initialize RPM DB as pkg src
    Pkg::TargetInit( "/",		// installed system
		     false ); 	// don't create a new RPM database
    
    // Pkg::SourceCreate( "file:/mounts/dist/install/stable-x86/", "" );
    Pkg::SourceCreate( "ftp://ftp.gwdg.de/pub/suse/update/10.2", "" );
    
    void detailedSelection()
    {
	// Open empty dialog for instant feedback
	
	UI::OpenDialog(`opt(`defaultsize),
		       `ReplacePoint(`id( `rep),
				     `Label( "Reading package database..." )
				     )
		       );

	// This will take a while: Detailed package data are retrieved
	// while the package manager is initialized  
	UI::ReplaceWidget(`rep, `PackageSelector(`id(`packages ),
						 `opt(`youMode),
						 "/dev/fd0" )
			  );
		
	symbol input = (symbol) UI::RunPkgSelection(`id(`packages ) );
	y2milestone( "Package selector returned  %1", input );
	UI::CloseDialog();
	    
    }


    
    if ( ! UI::HasSpecialWidget(`SimplePatchSelector ) )
    {
	detailedSelection();	// Fallback: Do detailed selection right away
	return;
    }

    
    UI::OpenDialog(`opt(`defaultsize), `SimplePatchSelector(`id(`selector) ) );
    
    symbol button = nil;

    repeat
	{
	    button = (symbol) UI::RunPkgSelection(`id(`selector) );
	    y2milestone( "SimplePatchSelector selector returned %1", button );

	    if ( button == `details )
		detailedSelection();
	    
	} until ( button == `cancel || button == `accept );
    
    UI::CloseDialog();
}
