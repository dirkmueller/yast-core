{

integer MAXVALUE = 100;
if ( size (WFM::Args()) > 0 )
{
    MAXVALUE = WFM::Args(0);
}

UI::{

    integer i = 0;
    list itemlist = [];

    y2debug( "START creating ITEMLIST" );
    repeat {
	itemlist = add( itemlist, `item(`id(i), "abcdefghijklm", 0, i ) );
	i = i+1;
	y2debug( "ITEM added: ", i );
    } until( i > MAXVALUE );

    y2debug( "ITEMLIST done - open dialog" );

    OpenDialog(
	       `VBox(
		     `HSpacing(50),	// make the dialog wider
		     `Label("Double-click any item to increase the amount column."),
		     `HBox(
			   `VSpacing(10),	// make the table higher
			   `Table(`id(`table),
				  `opt(`notify, `hvstretch),
				  `header("Name", "Amount", "Number"),
				  itemlist
				  )
			   ),
		     `PushButton(`id(`cancel), "&Close")
		     )
	       );
    y2debug( "Dialog opened with ITEMLIST" );

    while ( UserInput() != `cancel)
    {
	integer current_item  = QueryWidget(`id(`table), `CurrentItem);
	integer current_value = tointeger(select(QueryWidget(`id(`table), `Item(current_item)), 2, 0));
	ChangeWidget(`id(`table), `Item(current_item, 1), current_value+1);
    }

    CloseDialog();
};

}
