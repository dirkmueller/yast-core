#!/usr/bin/perl -w

#
# File:
#   ag_background 
#
# Authors:
#   Ladislav Slezak <lslezak@suse.cz>
#
# Description:
#   Background process agent 
#
# $Id$
#


use lib "/usr/lib/YaST2/agents_non_y2";
use ycp;
use strict;

use IPC::Open3;

# child running flag
our $child_running = 0;
our $child_pid = 0;

# max. size of input buffer (number of lines)
my $buffer_size = 500;    

# max. size of input buffer for error output
my $buffer_size_err = 200;

# input line buffer for stdin - store read part of the line
my $input1 = '';

# input line buffer for stderr - store read part of the line
my $input2 = '';

# SIGCHLD handler
sub Handler()
{
    if ($child_running)
    {
        $child_running = 0;
    }
}

# send SIGTERM (and then SIGKILL) to running subprocess
sub KillSubprocess()
{
    if ($child_running)
    {
	kill(15, $child_pid);       # send SIGTERM

	sleep 1;                    # wait a little bit

	if ($child_running)         # child process still running?
	{
	    kill(9, $child_pid);    # send SIGKILL
	}
	return 1;
    }
    else
    {
	return 0;
    }
}

sub ErrorPath($)
{
    my ($p) = @_;

    y2debug("Bad path: $p");
    ycpReturnSkalarAsString("");
}

sub ErrorCommand($)
{
    my ($c) = @_;
    
    y2debug("Bad command: $c");
    ycpReturnSkalarAsString("");
}

sub ErrorArg($)
{
    my ($a) = @_;
    
    y2debug("Bad argument type: $a");
    ycpReturnSkalarAsString("");
}

# install SIGCHLD handler
$SIG{CHLD} = \&Handler;

my $stdin_set = my $pipe_set = '';
vec($stdin_set, fileno(STDIN), 1) = 1;

my $watch = $stdin_set;	# watch STDIN

my $total_lines = 0;	# total number of lines from script
my $newlines = 0;	# number of new lines since last reading output
my $total_lines_err = 0;	# total number of lines from script
my $newlines_err = 0;	# number of new lines since last reading output

my $store_out = 0;
my $store_err = 0;
my @out = ();		# new lines since last reading
my @err = ();

my $pipe_defined = 0;
my $pipe_defined_err = 0;

my $exit = 0;

$| = 1;

# main loop
while (1) 
{
    # wait until some data can be read
    select(my $watch_out = $watch, undef, undef, undef);

    # get status
    my $stdin_avail = vec($watch_out, fileno(STDIN), 1);
    my $output_avail = ($pipe_defined == 1) ? vec($watch_out, fileno(RD), 1) : 0;
    my $err_avail = ($pipe_defined_err == 1) ? vec($watch_out, fileno(ERR), 1) : 0;
    my $line;

#    print "stdin_avail: $stdin_avail  output_avail: $output_avail   err_avail: $err_avail\n";

    if ($stdin_avail)	# STDIN data available
    {
	$line = <STDIN>;

	if (!defined $line)
	{
	    exit;
	}
	
	chomp($line);

	ycpInit($line);
	
	my $path = ycpGetPath();

	if (ycpCommandIsExecute())
	{
	    if ($path eq '.run' || $path eq '.run_output' || $path eq '.run_output_err')
	    {
		# another process is already running
		if ($child_running)
		{
		    ycpReturnSkalarAsBoolean(0);
		}
		elsif (ycpArgIsString())
		{
		    my $param = ycpGetArgString();
		
		    $store_out = ($path eq '.run') ? 0 : 1;
		    $store_err = ($path eq '.run_output_err') ? 1 : 0;

		    #reset counters
		    $total_lines = 0;
		    $newlines = 0;
		    @out = ();

		    $total_lines_err = 0;
		    $newlines_err = 0;
		    @err = ();

		    $child_running = 1;

		    my $start_error = 0;

		    # start subprocess
		    $child_pid = open3(*WRT, *RD, *ERR, "$param")
			or $start_error = 1;

		    # if child is running or 
		    if ($start_error == 0)
		    {
			$pipe_set = '';
			vec($pipe_set, fileno(RD), 1) = 1;
			vec($pipe_set, fileno(ERR), 1) = 1;

			$watch = $stdin_set | $pipe_set;       # watch both sets
			$pipe_defined = 1;
			$pipe_defined_err = 1;
		    }

		    # use non-blocking write
		    use Fcntl;
		    fcntl(WRT, F_SETFL, O_NONBLOCK);

		    ycpReturnSkalarAsBoolean(!$start_error);
		}
		else
		{
		    ErrorArg(ycpGetArgType());
		}
	    }
	    elsif ($path eq '.kill')
	    {
    		ycpReturnSkalarAsBoolean(KillSubprocess());
	    }
	    else
	    {
		ErrorPath($path);
	    }
	}
	elsif (ycpCommandIsRead())
	{
	    if ($path eq '.lines')
	    {
		ycpReturnSkalarAsInt($total_lines);
	    }
	    elsif ($path eq '.newlines')
	    {
		ycpReturnSkalarAsInt($newlines);
	    }
	    elsif ($path eq '.newlines_err')
	    {
		ycpReturnSkalarAsInt($newlines_err);
	    }
	    elsif ($path eq '.lines_err')
	    {
		ycpReturnSkalarAsInt($total_lines_err);
	    }
	    elsif ($path eq '.store')
	    {
		ycpReturnSkalarAsBoolean($store_out);
	    }
	    elsif ($path eq '.isrunning')
	    {
		ycpReturnSkalarAsBoolean($child_running);
	    }
	    elsif ($path eq '.status')
	    {
		ycpReturnSkalarAsInt($exit);
	    }
	    elsif ($path eq '.newout')
	    {
		ycpReturnArrayAsList(@out);
		
		$newlines = 0;
		@out = ();
	    }
	    elsif ($path eq '.newerr')
	    {
		ycpReturnArrayAsList(@err);
		
		$newlines_err = 0;
		@err = ();
	    }
	    elsif ($path eq '.buffer_out')
	    {
		ycpReturnSkalarAsString($input1);
		$input1 = '';
	    }
	    elsif ($path eq '.buffer_err')
	    {
		ycpReturnSkalarAsString($input2);
		$input2 = '';
	    }
	    elsif ($path eq '.output_open')
	    {
		ycpReturnSkalarAsBoolean($pipe_defined);
	    }
	    elsif ($path eq '.buffer_size')
	    {
		ycpReturnSkalarAsInt($buffer_size);
	    }
	    elsif ($path eq '.buffer_size_err')
	    {
		ycpReturnSkalarAsInt($buffer_size_err);
	    }
	    else
	    {
		ErrorPath($path);
	    }
	}
	elsif (ycpCommandIsResult)
	{
	    if ($child_running)
	    {
		KillSubprocess();
	    }
	    exit(0);
	}
	elsif (ycpCommandIsWrite())
	{
	    if ($path eq '.buffer_size' || $path eq '.buffer_size_err')
	    {
		if (ycpArgIsInteger())
		{
		    my $param = ycpGetArgInteger();

		    if ($param >= 1)
		    {
			if ($path eq '.buffer_size')
			{
			    $buffer_size = $param;
			}
			else
			{
			    $buffer_size_err = $param;
			}

			ycpReturnSkalarAsBoolean(1);
		    }
		    else
		    {
			ycpReturnSkalarAsBoolean(0);
		    }
		}
		else
		{
		    ErrorArg(ycpGetArgType());
		}
	    }
	    elsif ($path eq '.stdin')
	    {
		if ($child_running)
		{
		    ycpReturnSkalarAsInt(syswrite(WRT, ycpGetArgString()."\n"));	# add new line char
		}
		else
		{
		    ycpReturnSkalarAsInt(-1);
		}
	    }
	    else
	    {
		ErrorPath($path);
	    }
	}
	else
	{
	    ErrorCommand(ycpGetCommand());
	}
    }
	    
    if ($output_avail) 	# script output available
    {
	if ($newlines >= $buffer_size)
	{
	    # too many lines in input buffer - wait for STDIN command
	    select(my $watch_out = $stdin_set, undef, undef, undef);
	}
	else
	{
	    my $len = sysread(RD, $line, 64);
	    $input1 = "$input1$line";
	    
	    my $pos = index($input1, "\n");
	    
	    if ((defined $len) && $len == 0)
	    {
		$pipe_defined = 0;
		$watch = $stdin_set;	# now watch only STDIN

		# watch also stderr if it is still open
		if ($pipe_defined_err)
		{
		    vec($watch, fileno(ERR), 1) = 1;
		}

		close(RD);
		$exit = $? >> 8;	# high 8 bits are exit value
	    }
	    else
	    {
		while ($pos >= 0)
		{
		    $line = substr($input1, 0, $pos);
		    $input1 = substr($input1, $pos + 1);

		    if ($store_out)
		    {
			push(@out, $line);
		    }
		    
		    $newlines++;
		    $total_lines++;

		    $pos = index($input1, "\n");
		}
	    }
	}
    }

    if ($err_avail) 	# error output available
    {
	if ($newlines_err >= $buffer_size_err)
	{
	    # too many lines in input buffer - wait for STDIN command
	    select(my $watch_out = $stdin_set, undef, undef, undef);
	}
	else
	{
	    my $len = sysread(ERR, $line, 64);
	    $input2 = "$input2$line";
	    
	    my $pos = index($input2, "\n");
	    
	    if ($len == 0)
	    {
		$pipe_defined_err = 0;
		$watch = $stdin_set;	# now watch only STDIN

		# watch also stdout if it is still open
		if ($pipe_defined)
		{
		    vec($watch, fileno(RD), 1) = 1;
		}

		close(ERR);
		$exit = $? >> 8;	# high 8 bits are exit value
	    }
	    else
	    {
		while ($pos >= 0)
		{
		    $line = substr($input2, 0, $pos);
		    $input2 = substr($input2, $pos + 1);

		    if ($store_err)
		    {
			push(@err, $line);
		    }
		    
		    $newlines_err++;
		    $total_lines_err++;

		    $pos = index($input2, "\n");
		}
	    }
	}
    }
}


